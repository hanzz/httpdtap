#! /usr/bin/env stap

/*
 * Summary: Shows files opened during the request.
 * Description:
 * -> req_files <show-failed> - Shows also failed tries to open particular file.
 */

/*
 * Licensed to the Apache Software Foundation (ASF) under one
 * or more contributor license agreements.  See the NOTICE file
 * distributed with this work for additional information
 * regarding copyright ownership.  The ASF licenses this file
 * to you under the Apache License, Version 2.0 (the
 * "License"); you may not use this file except in compliance
 * with the License.  You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied.  See the License for the
 * specific language governing permissions and limitations
 * under the License.
 *
 * Author: Jan Kaluza
 */

global request

probe begin {
	printf("Showing files accessed by requests.\n")
}

probe process("/usr/*bin/httpd").function("ap_process_request") {
	now = gettimeofday_us()
	printf("[%d] [tid=%d] Handling new request: %s\n", now, tid(), user_string($r->unparsed_uri))
	request[tid()] = $r
}

probe syscall.open.return {
	now = gettimeofday_us()
	if ([tid()] in request) {
		filename = user_string($filename)
		if ($return >= 0) {
			printf("[%d] [tid=%d] Opened: %s\n", now, tid(), filename)
		}
%( $# == 2 %?
		else if (@2 == "show-failed") {
			printf("[%d] [tid=%d] Opening failed: %s\n", now, tid(), filename)
		}
%)
	}
}

probe process("/usr/*bin/httpd").function("ap_pass_brigade") {
	if ([tid()] in request) {
		if (@cast($next->r, "request_rec")->output_filters == $next) {
			now = gettimeofday_us()
			printf("[%d] [tid=%d] Request handled: %s\n", now, tid(), user_string(@cast($next->r, "request_rec")->unparsed_uri))
			delete request[tid()]
		}
	}
}

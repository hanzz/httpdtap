#! /usr/bin/env stap

/*
 * Summary: Shows requests slower than N ms.
 * Description:
 * -> req_slower_than <ms> [with_download]
 * -> with_download: count also the time needed by client to fully download
 *                   the response.
 */

/*
 * Licensed to the Apache Software Foundation (ASF) under one
 * or more contributor license agreements.  See the NOTICE file
 * distributed with this work for additional information
 * regarding copyright ownership.  The ASF licenses this file
 * to you under the Apache License, Version 2.0 (the
 * "License"); you may not use this file except in compliance
 * with the License.  You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied.  See the License for the
 * specific language governing permissions and limitations
 * under the License.
 *
 * Author: Jan Kaluza
 */

global pending_requests[2048]
global requests_uri[2048]

probe begin {
%( $# < 2 %?
	printf("Missing number of ms!\n")
	printf("Syntax: req_slower_than <ms>\n")
	exit()
%)

%( $# >= 2 %?
	printf("Showing requests slower than %d ms\n", $2)
%)
}

probe process("/usr/*bin/httpd").function("ap_process_request") {
	pending_requests[pid(), tid(), $r->connection] = gettimeofday_ms()
	requests_uri[pid(), tid(), $r->connection] = user_string($r->the_request)
}

/* Version without "with_download" option */
%( $# == 2 %?

probe process("/usr/*bin/httpd").function("ap_pass_brigade") {
	if ($next->r) {
		conn = @cast($next->r, "request_rec")->connection
		if ([pid(), tid(), conn] in pending_requests) {
			if (@cast($next->r, "request_rec")->output_filters == $next) {
				now = gettimeofday_ms()
				diff = now - pending_requests[pid(), tid(), conn]
				if (diff > $2) {
					printf("Slow request took %d ms: %s\n", diff, requests_uri[pid(), tid(), conn])
				}
				delete pending_requests[pid(), tid(), conn]
				delete requests_uri[pid(), tid(), @cast($next->r, "request_rec")->connection]
			}
		}
	}
}

%)

/* Version with "with_download" option */
%( $# == 3 %?

probe process("/usr/*bin/httpd").function("ap_finalize_request_protocol") {
	if ([pid(), tid(), $r->connection] in pending_requests) {
		now = gettimeofday_ms()
		diff = now - pending_requests[pid(), tid(), $r->connection]
		if (diff > $2) {
			printf("Slow request took %d ms: %s\n", diff, user_string($r->the_request))
		}
		delete pending_requests[pid(), tid(), $r->connection]
		delete requests_uri[pid(), tid(), $r->connection]
	}
}

%)

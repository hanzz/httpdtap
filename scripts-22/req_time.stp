#! /usr/bin/env stap

/*
 * Summary: Shows requests sorted by time needed to finished them.
 * Description:
 * Syntax: req_time [with_download]
 * - with_download: count also the time needed by client to fully download
 * the response.
 * Prints 10 slowest requests together with the time needed to handle them.
 * Also prints the requests grouped by URI together with avg/min/max time
 * per each URI.
 */

global pending_requests[2048]
global requests[2048]
global requests_uri[2048]
global st[512]
global sorted[512]

probe process("/usr/*bin/httpd").function("ap_process_async_request") {
    requests_uri[pid(), tid(), $r->connection] = user_string($r->unparsed_uri)
	pending_requests[pid(), tid(), $r->connection] = gettimeofday_us()
}

/* Version without "with_download" option */
%( $# == 1 %?

probe process("/usr/*bin/httpd").function("ap_process_request_after_handler") {
	if ([pid(), tid(), $r->connection] in pending_requests) {
		uri = user_string($r->unparsed_uri)
		now = gettimeofday_us()
		diff = now - pending_requests[pid(), tid(), $r->connection]
		delete pending_requests[pid(), tid(), $r->connection]
		delete requests_uri[pid(), tid(), $r->connection]
		requests[pid(), tid(), uri, $r->connection, now] = diff
		st[uri] <<< diff
	}
}

%)

/* Version with "with_download" option */
%( $# == 2 %?

probe process("/usr/*bin/httpd").function("ap_read_request") {
	if ([pid(), tid(), $conn] in pending_requests) {
		now = gettimeofday_us()
		uri = requests_uri[pid(), tid(), $conn]
		diff = now - pending_requests[pid(), tid(), $conn]
		delete pending_requests[pid(), tid(), $conn]
		delete requests_uri[pid(), tid(), $r->connection]
		requests[pid(), tid(), uri, $conn, now] = diff
		st[uri] <<< diff
	}
}

%)

probe end {
	i = 1
	printf("10 slowest requests:\n")
	foreach (v = [pid, tid, uri, conn, now] in requests- limit 10) {
		printf("   %d. '%s': %d us\n", i, uri, v)
		i = i + 1
	}

	foreach (k in st) {
		sorted[k] = @avg(st[k])
	}

	i = 1
	printf("Grouped requests by URI:\n")
	foreach (v = [k] in sorted-) {
		printf("   %d. '%s': avg=%d us, min=%d us, max=%d us\n", i, k, @avg(st[k]), @min(st[k]), @max(st[k]))
		i = i + 1
	}
}

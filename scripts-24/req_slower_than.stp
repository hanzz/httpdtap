#! /usr/bin/env stap

/*
 * Summary: Shows requests slower than N ms.
 * Description:
 * Syntax: req_slower_than <ms> [with_download]
 * - with_download: count also the time needed by client to fully download
 * the response.
 * Prints Requests slower than N ms.
 */

global pending_requests[2048]
global requests_uri[2048]

probe begin {
%( $# < 2 %?
	printf("Missing number of ms!\n")
	printf("Syntax: req_slower_than <ms>\n")
	exit()
%)

%( $# >= 2 %?
	printf("Showing requests slower than %d ms\n", $2)
%)
}

probe process("/usr/*bin/httpd").function("ap_process_async_request") {
	pending_requests[pid(), tid(), $r->connection] = gettimeofday_ms()
	requests_uri[pid(), tid(), $r->connection] = user_string($r->the_request)
}

/* Version without "with_download" option */
%( $# == 2 %?

probe process("/usr/*bin/httpd").function("ap_process_request_after_handler") {
	if ([pid(), tid(), $r->connection] in pending_requests) {
		now = gettimeofday_ms()
		diff = now - pending_requests[pid(), tid(), $r->connection]
		if (diff > $2) {
			printf("Slow request took %d ms: %s\n", diff, user_string($r->the_request))
		}
		delete pending_requests[pid(), tid(), $r->connection]
		delete requests_uri[pid(), tid(), $r->connection]
	}
}

%)

/* Version with "with_download" option */
%( $# == 3 %?

probe process("/usr/*bin/httpd").function("ap_read_request") {
	if ([pid(), tid(), $conn] in pending_requests) {
		now = gettimeofday_ms()
		diff = now - pending_requests[pid(), tid(), $conn]
		if (diff > $2) {
			printf("Slow request took %d ms: %s\n", diff, requests_uri[pid(), tid(), $conn])
		}
		delete pending_requests[pid(), tid(), $conn]
		delete requests_uri[pid(), tid(), $r->connection]
	}
}

%)

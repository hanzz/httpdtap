#! /usr/bin/env stap

/*
 * Summary: Shows requests sorted by time needed to finished them.
 * Description:
 * Prints 10 slowest requests together with the time needed to handle them.
 * Also prints the requests grouped by URI together with avg/min/max time
 * per each URI.
 */

global pending_requests[2048]
global requests[2048]
global st[512]
global sorted[512]

probe process("/usr/sbin/httpd").function("ap_process_async_request") {
	uri = user_string($r->uri)
	pending_requests[pid(), tid(), uri, $r] = gettimeofday_us()
}

probe process("/usr/sbin/httpd").function("ap_process_request_after_handler") {
	uri = user_string($r->uri)
	if ([pid(), tid(), uri, $r] in pending_requests) {
		now = gettimeofday_us()
		diff = now - pending_requests[pid(), tid(), uri, $r]
		delete pending_requests[pid(), tid(), uri, $r]
		requests[pid(), tid(), uri, $r, now] = diff
		st[uri] <<< diff
	}
}

probe end {
	i = 1
	printf("10 slowest requests:\n")
	foreach (v = [pid, tid, uri, r, now] in requests- limit 10) {
		printf("   %d. '%s': %d us\n", i, uri, v)
		i = i + 1
	}

	foreach (k in st) {
		sorted[k] = @avg(st[k])
	}

	i = 1
	printf("Grouped requests by URI:\n")
	foreach (v = [k] in sorted-) {
		printf("   %d. '%s': avg=%d us, min=%d us, max=%d us\n", i, k, @avg(st[k]), @min(st[k]), @max(st[k]))
		i = i + 1
	}
}

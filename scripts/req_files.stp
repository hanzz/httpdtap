#! /usr/bin/env stap

/*
 * Summary: Shows files opened during the request.
 */

global request

probe process("/usr/sbin/httpd").function("ap_process_async_request") {
	now = gettimeofday_us()
	printf("[%d] [pid=%d, tid=%d] Handling new request: %s\n", now, pid(), tid(), user_string($r->unparsed_uri))
	request[pid(), tid()] = $r
}

probe syscall.open.return {
	now = gettimeofday_us()
	if ([pid(), tid()] in request) {
		filename = user_string($filename)
		printf("[%d] [pid=%d, tid=%d] Opening: %s\n", now, pid(), tid(), filename)
	}
}

probe process("/usr/sbin/httpd").function("ap_process_request_after_handler") {
	if ([pid(), tid()] in request) {
		now = gettimeofday_us()
		printf("[%d] [pid=%d, tid=%d] Request handled: %s\n", now, pid(), tid(), user_string($r->unparsed_uri))
		delete request[pid(), tid()]
	}
}
